plugins {
	id 'org.springframework.boot' version '2.3.2.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'



configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	cmddep
}

repositories {
	mavenCentral()
}

task mbg {
	doLast {
		List<File> files = configurations.cmddep.toList()
		String jdbcDriver = files.find {
			return it.name.contains("mysql-connector-java")
		}

		println "${jdbcDriver};${projectDir}/external/mybatis-generator-core-1.4.0/lib/mybatis-generator-core-1.4.0.jar"
		exec {
			commandLine 'java',
					'-cp', "${jdbcDriver}:${projectDir}/external/mybatis-generator-core-1.4.0/lib/mybatis-generator-core-1.4.0.jar",
					'org.mybatis.generator.api.ShellRunner',
					'-configfile', './external/MyBatisGenerateConfig.xml',
					'-overwrite'
		}
	}
}

dependencies {
	cmddep 'mysql:mysql-connector-java:8.0.20'

	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.1.3'
//	implementation 'org.apache.shiro:shiro-spring-boot-web-starter:1.5.3'

	runtimeOnly 'mysql:mysql-connector-java'
	compileOnly 'org.projectlombok:lombok:1.18.12'
	annotationProcessor 'org.projectlombok:lombok:1.18.12'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}
